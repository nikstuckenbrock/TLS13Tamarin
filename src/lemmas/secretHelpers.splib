lemma one_C2d_per_tid [reuse]:
    "All tid #i #j. C2d(tid)@i & C2d(tid)@j ==> #i = #j"

lemma one_C2d_PSK_per_tid [reuse]:
    "All tid #i #j. C2d_PSK(tid)@i & C2d_PSK(tid)@j ==> #i = #j"

lemma one_S2d_per_tid [reuse]:
    "All tid #i #j. S2d(tid)@i & S2d(tid)@j ==> #i = #j"

lemma one_S2d_PSK_per_tid [reuse]:
    "All tid #i #j. S2d_PSK(tid)@i & S2d_PSK(tid)@j ==> #i = #j"

lemma one_C3_per_tid [reuse]:
    "All tid #i #j. C3(tid)@i & C3(tid)@j ==> #i = #j"

lemma one_C3_cert_per_tid [reuse]:
    "All tid #i #j. C3_cert(tid)@i & C3_cert(tid)@j ==> #i = #j"

lemma one_S3_per_tid [reuse]:
    "All tid #i #j. S3(tid)@i & S3(tid)@j ==> #i = #j"

lemma one_S3_cert_per_tid [reuse]:
    "All tid #i #j. S3_cert(tid)@i & S3_cert(tid)@j ==> #i = #j"

lemma S3_vs_S3_cert [reuse]:
    "All tid #i #j. S3(tid)@i & S3_cert(tid)@j ==> F"

lemma C3_vs_C3_cert [reuse]:
    "All tid #i #j. C3(tid)@i & C3_cert(tid)@j ==> F"

lemma S2d_vs_S2d_PSK [reuse]:
    "All tid #i #j. S2d(tid)@i & S2d_PSK(tid)@j ==> F"

lemma C2d_vs_C2d_PSK [reuse]:
    "All tid #i #j. C2d(tid)@i & C2d_PSK(tid)@j ==> F"

lemma tid_invariant [use_induction, reuse]:
  "All tid actor role #i. Instance(tid, actor, role)@i==>
      (Ex #j. Start(tid, actor, role)@j & (#j < #i))"

lemma one_start_per_tid [reuse]:
  "All tid actor actor2 role role2 #i #j. Start(tid, actor, role)@i & Start(tid, actor2, role2)@j ==>#i=#j"

lemma cert_req_origin [typing]:
  "All certificate_request_context certificate_extensions keys #i.
    KU(senc{<'13',certificate_request_context,certificate_extensions>}keys)@i ==> 
      (Ex #j. KU(certificate_request_context)@j & #j < #i) |
      (Ex #j tid actor role. RCertReqCtxt(tid, actor, role, <certificate_request_context>)@j & #j < #i)"
lemma nst_source [typing]:
  "All ticket ticket_age_add tkt_lt tkt_exts app_key #i.
    KU(senc{<'4',tkt_lt,ticket_age_add,ticket,tkt_exts>}app_key)@i ==>
      (Ex #j #k. KU(ticket)@j & KU(ticket_age_add)@k & #j < #i & #k < #i) |
      (Ex tid S #j. RNST(tid, S, 'server', <ticket,ticket_age_add>)@j & #j < #i)"
lemma ku_extract [reuse, use_induction]:
  "All a b #i. KU(Extract(a, b))@i ==> Ex #j #k. KU(a)@j & KU(b)@k & #j < #i & #k < #i"

lemma ku_expand [reuse, use_induction]:
"All secret label len #i. KU(Expand(secret, label, len))@i ==>
  (Ex #j. KU(secret)@j & #j < #i) |
  (not (Ex #k. KU(secret)@k & #k < #i) &
  (Ex actor #l. RevealPSK(actor, Expand(secret, label, len))@l & #l < #i))"

lemma dh_chal_dual [reuse]:
  "All tid tid2 actor actor2 g x y gx gy gxy #i #j #r.
      DHChal(g, x, y, gx, gy, gxy)@i & Instance(tid, actor, 'client')@i &
      DHChal(g, x, y, gx, gy, gxy)@j & Instance(tid2, actor2, 'server')@j &
      KU(gxy)@r 
      ==> 
      (Ex #p. (RevDHExp(tid, actor,  x)@p & #p < #r)) | 
      (Ex #q. (RevDHExp(tid2, actor2, y)@q & #q < #r))"
lemma ku_hs [reuse]:
  "All tid actor role es hs res_psk gxy #i #j.
    RHS(tid, actor, role, <hs>)@i &
    hs = Extract(gxy, es) &
    es = Extract(res_psk, '0') &
    KU(hs)@j ==>
      Ex #k #l. KU(gxy)@k & KU(res_psk)@l & #k < #j & #l < #j"

lemma dh_exp_invariant  [use_induction, reuse]:
  "All tid actor x #i. RevDHExp(tid, actor, x)@i ==>
    Ex #j. DH(tid, actor, x)@j & #j < i"

lemma ku_ltk [reuse]:
  "All actor ltkA #i #j.
    GenLtk(actor, ltkA)@i & KU(ltkA)@j ==>
      Ex #k. RevLtk(actor)@k & #k < #j"

lemma ku_fresh_psk [reuse]:
  "All ticket res_psk #i #k.
      FreshPSK(ticket,res_psk)@i & KU(res_psk)@k ==> 
        Ex actor #j. 
          RevealPSK(actor, res_psk)@j & #j < #k"

lemma hsms_derive [reuse]:
  "All tid actor role hs ms #i. 
    RHSMS(tid, actor, role, <hs,ms>)@i ==>
      ms = Extract('0', hs)"

lemma posths_rms [reuse, use_induction]:
  "All tid actor role hs rms peer auth_status messages #i. 
    RPostHS(tid, actor, role, <hs,rms,peer,auth_status,messages>)@i ==>
      Ex aas pas ms #j. 
                RRMS(tid, actor, role, <peer,rms,messages>)@j &
                ms = Extract('0', hs) & rms = Expand(ms, <'rms', <h(messages)>>, '32') & #j < #i &
                auth_status = <aas, pas> &
      (
        (Ex aas2 #k. CIdentity(tid, actor, role, <peer,<aas2,pas>>)@k & #k < #i) |
        (Ex aas2 #k. CIdentityPost(tid, actor, role, <peer,<aas2,pas>>)@k &
                role = 'server' & pas = 'auth' & (#k < #i | #k = #i)
        )
      )"

lemma posths_rms_weak [reuse, use_induction]:
  "All tid actor role hs rms peer auth_status messages #i. 
    RPostHS(tid, actor, role, <hs,rms,peer,auth_status,messages>)@i ==>
      Ex aas pas ms #j. 
                RRMS(tid, actor, role, <peer,rms,messages>)@j &
                ms = Extract('0', hs) & rms = Expand(ms, <'rms', <h(messages)>>, '32') & #j < #i &
                auth_status = <aas, pas>"


lemma matching_transcripts_posths [reuse]:
  "All tid tid2 actor peer actor2 peer2 role role2 rms rms2 messages #i #j.
    RRMS(tid, actor, role, <peer2,rms,messages>)@i &
    RRMS(tid2, peer, role2, <actor2,rms2,messages>)@j & not (role = role2) ==>
     rms = rms2"

lemma matching_rms_posths [reuse]:
  "All tid tid2 actor peer actor2 peer2 role role2 rms messages messages2 #i #j.
    RRMS(tid, actor, role, <peer2,rms,messages>)@i &
    RRMS(tid2, peer, role2, <actor2,rms,messages2>)@j & not (role = role2) ==>
     messages = messages2"


lemma matching_rms_actors [reuse]:
  "All tid tid2 actor peer actor2 peer2 role rms messages messages2 #i #j.
    RRMS(tid, actor, role, <peer,rms,messages>)@i &
    RRMS(tid2, actor2, role, <peer2,rms,messages2>)@j ==>
     actor = actor2 & tid = tid2"

lemma rev_dh_before_hs  [reuse]:
  "All tid actor role hs x #i #j.
    RHS(tid, actor, role, <hs>)@j &
    RevDHExp(tid, actor, x)@i ==>
    #i < #j"

lemma matching_sessions [reuse, use_induction, hide_lemma=posths_rms]:
  "All tid tid2 actor actor2 role role2 peer peer2 rms messages #i #j #k.
    RRMS(tid, actor, role, <peer2,rms,messages>)@i & 
    RRMS(tid2, peer, role2, <actor2,rms,messages>)@j &
    not (role = role2) &
    KU(rms)@k ==>
      (Ex tid3 x #r. RevDHExp(tid3, actor, x)@r & #r < #i) |
      (Ex tid4 y #r. RevDHExp(tid4, peer, y)@r & #r < #j) |
      (Ex rms2 #r. RevealPSK(actor, rms2)@r & #r < #k) |
      (Ex rms2 #r. RevealPSK(peer, rms2)@r & #r < #k)"

lemma sig_origin [reuse]:
  "All certificate certificate_request_context signature verify_data hs_key sig_messages ltkA  #i.
        KU(senc{<'11',certificate_request_context,certificate>, <'15',  signature >, <'20',verify_data>}hs_key)@i & (signature = sign{sig_messages}ltkA) ==>
      (Ex #j. KU(ltkA)@j & #j < i) | (Ex #k. UseLtk(ltkA, signature)@k & #k < #i)"

lemma post_master_secret [reuse, hide_lemma=posths_rms]:
  "All tid actor peer role hs rms aas messages #i #k.
    RPostHS(tid, actor, role, <hs,rms,peer,<aas,'auth'>,messages>)@i & 
    CHS(tid, actor, role, <hs>)@i & 
    CIdentityPost(tid, actor, role, <peer,<aas,'auth'>>)@i &
    KU(rms)@k ==>
      (Ex #r. RevLtk(peer)@r & #r < #i) |
      (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
      (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
      (Ex rms2 #r. RevealPSK(actor, rms2)@r & #r < #k) |
      (Ex rms2 #r. RevealPSK(peer, rms2)@r & #r < #k)"

lemma invariant_post_hs [reuse, use_induction, hide_lemma=posths_rms]:
  "All tid actor peer peer2 role hs hs2 rms rms2 as as2 msgs msgs2 #i #j.
    RPostHS(tid, actor, role, <hs,rms,peer,as,msgs>)@i & 
    RPostHS(tid, actor, role, <hs2,rms2,peer2,as2,msgs2>)@j ==>
      peer = peer2 & rms = rms2 & msgs = msgs2 & hs = hs2"

lemma auth_psk [reuse, use_induction, hide_lemma=posths_rms_weak]:
  "All tid tid2 actor actor2 role role2 peer peer2 rms messages aas #i #j #k.
    RRMS(tid, actor, role, <peer2,rms,messages>)@i & 
    RRMS(tid2, peer, role2, <actor2,rms,messages>)@j &
    CIdentity(tid, actor, role, <peer2,<aas,'auth'>>)@k &
    not (role = role2)
     ==>
      peer2 = peer |
      Ex #r. RevLtk(peer2)@r & #r < #k"sig_origin

lemma rev_dh_ordering  [reuse, use_induction]:
  "All tid actor x #j. 
    DeleteDH(tid, actor, x)@j==>
      ((Ex #i. DH(tid, actor, x) @ i & i < j) & 
       (All #r. RevDHExp(tid, actor, x) @ r  ==>  r < j))"

lemma matching_hsms [reuse]:
  "All tid actor role hs hs2 ms #i #j.
    CHS(tid, actor, role, <hs2>)@i &
    RHSMS(tid, actor, role, <hs,ms>)@j ==>
      hs = hs2"

lemma handshake_secret [reuse, use_induction, hide_lemma=posths_rms_weak]:
  "All tid actor peer role hs aas #i #k.
    CHS(tid, actor, role, <hs>)@i &
    CIdentity(tid, actor, role, <peer,<aas,'auth'>>)@i &
    KU(hs)@k ==>
        (Ex #r. RevLtk(peer)@r & #r < #i) |
        (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
        (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
        (Ex rms #r. RevealPSK(actor, rms)@r & #r < #k) |
        (Ex rms #r. RevealPSK(peer, rms)@r & #r < #k)"

lemma pfs_handshake_secret [reuse, hide_lemma=posths_rms_weak]:
  "All tid actor peer role hs aas psk_ke_mode #i #k.
    CHS(tid, actor, role, <hs>)@i &
    RMode(tid, actor, role, <psk_ke_mode>)@i &
    CIdentity(tid, actor, role, <peer,<aas,'auth'>>)@i &
    KU(hs)@k &
    (not psk_ke_mode = '0') ==>
        (Ex #r. RevLtk(peer)@r & #r < #i) |
        (Ex tid3 x #r. RevDHExp(tid3, peer, x)@r & #r < #i) |
        (Ex tid4 y #r. RevDHExp(tid4, actor, y)@r & #r < #i) |
        (Ex rms #r. RevealPSK(actor, rms)@r & #r < #i) |
        (Ex rms #r. RevealPSK(peer, rms)@r & #r < #i)"